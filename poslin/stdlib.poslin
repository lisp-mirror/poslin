0 ^ !
?eo !
~       ; ( -- [] )
[ 0 ^ & ; []
] @o !

~ !
?eo !
?~eo    ; ( -- op-env )
[ ~ &   ; []
  ?eo & ; op-env
] @o !

?~eo !
^^      ; ( -- [] )
[ 1 ^ & ; []
] @o !

?~eo !
^<-    ; ( [ ... val -- val [ ... )
[ ^^ & ; [ ... val []
  <> & ; [ ... [] val
  <- & ; val [ ...
] @o !

?~eo !
^->    ; ( val [ ... -- [ ... val )
[ ^^ & ; val [ ... []
  -> & ; [ ... val
] @o !

?~eo !
@~eo    ; ( op-env -- )
[ ~ &   ; op-env []
  <> &  ; [] op-env
  @eo & ;
] @o !

?~eo !
@~o      ; ( sym op -- )
[ >r &   ; sym
  ?~eo & ; sym op-env
  <> &   ; op-env sym
  r> &   ; op-env sym op
  @o &   ;
] @o !

?~o      ; ( oname -- thread )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  ?o &   ; thread
] @~o !

?~ev    ; ( -- var-env )
[ ~ &   ; []
  ?ev & ; var-env
] @~o !

@~ev    ; ( var-env -- )
[ ~ &   ; var-env []
  <> &  ; [] var-env
  @ev & ;
] @~o !

@~v      ; ( vname val -- )
[ >r &   ; vname
  ?~ev & ; vname var-env
  <> &   ; var-env vname
  r> &   ; var-env vname val
  @v &   ;
] @~o !

?~v      ; ( vname -- val )
[ ?~ev & ; vname var-env
  <> &   ; var-env vname
  ?v &   ; val
] @~o !

@~o_     ; ( oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @o_ &  ;
] @~o !

@~v_     ; ( vname -- )
[ ?~ev & ; vname var-env
  <> &   ; var-env vname
  @v_ &  ;
] @~o !

@~i+     ; ( oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @i+ &  ;
] @~o !

@~i_     ; ( oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @i_ &  ;
] @~o !

@~i-     ; (oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @i- &  ;
] @~o !

]&     ; ( [ ... -- thread )
[ '] & ; []
  '& & ; thread
] @~o !
]& @~i+ !

]@      ; ( sym [ ... -- )
[ '] &  ; sym []
  @~o & ;
] @~o !
]@ @~i+ !

]_     ; ( [ ... -- )
[ '] & ; []
  _ &  ;
]@
]_ @~i+ !

]i       ; ( sym [ ... -- )
[ '] &   ; sym []
  >r &   ; sym
  § &    ; sym sym
  r> &   ; sym sym []
  @~o &  ; sym
  @~i+ & ;
]@
]i @~i+ !

@~n    ; ( sym -- )
[ ~ &  ; sym []
  <> & ; [] sym
  @n & ;
]@

?~n    ; ( -- sname )
[ ~ &  ; []
  ?n & ; sname
]@

!&      ; ( op -- op ! )
[ ''! & ; op !
]i

<?>!       ; ( b op1 op2 -- b op1 op2 {<?>} {!} )
[ <?> '& & ; b op1 op2 {<?>}
  '!& &    ; b op1 op2 {<?>} {!}
]i


?~_    ; ( [     -- NIL )
       ; ( [ ... -- T   )
[ ~ &  ; [ ...? []
  ?_ & ; [ ...? b
]@

__ ; ( ... -- )
[ ?~_ &
    ||
    [ _ &
      __ !&
    ]
  <?>!
]@

{          ; ( sym -- [sym )
[ '[ &     ; sym [
     ^-> & ; [ sym
     @~n & ; [sym
]i

&&      ; ( op -- op & )
[ ''& & ; op &
]i

}                 ; ( [sname ... -- [sname {[} op-env {@eo+} {@~eo}
		  ;   	     	    	       var-env {@ev+} {@~ev}
		  ;			       sname {@~n}
		  ;			       {...} {]} ] )
[ '] &
  '[ &
     ''[ &
         ?~eo &
         @eo+ &&
         @~eo &&
         ?~ev &
         @ev+ &&
         @~ev &&
         ^-> &
         § &
         ^<- &
         ?n &
         @~n &&
         ^-> & &&
     '']_ &
  '] &
]i

}&
[ '} &
  &&
]i

}@
[ '] &
  § &
  ?n &
  <> &
  '%[ &
  '} &
  @~o &
]i

&@v
[ @~v &&
]i

&v
[ ?~v &&
]i

arg
[ ^-> &&
  '&@v &
]i

<<§    ; ( val1 val2 -- val1 val1 val2 )
[ >r & ; val1
  § &  ; val1 val1
  r> & ; val1 val1 val2
]@

<>§     ; ( val1 val2 -- val1 val2 val1 )
[ <<§ & ; val1 val1 val2
  <> &  ; val1 val2 val1
]@

<<>     ; ( val1 [ ... ] val2 -- [ ... val2 ] val1 )
[ <<§ & ; val1 [ ... ] [ ... ] val2
  <- &  ; val1 [ ... val2 ]
  <> &  ; [ ... val2 ] val1
]@

<>>    ; ( [ ... val ] -- val [ ... ] )
[ § &  ; [ ... val ] [ ... val ]
  -> & ; [ ... ] val
  <> & ; val [ ... ]
]@

?^ev     ; ( -- var-env )
[ ?~ev &
  ?ev- &
]@

?^v      ; ( sym -- val )
[ ?^ev & ; sym var-env
  <> &   ; var-env sym
  ?v &   ; val
]@

&^v
[ ?^v &&
]i

@^v      ; ( name val -- )
[ >r &   ; name
  ?^ev & ; name var-env
  <> &   ; var-env name
  r> &   ; var-env name val
  @v &   ;
]@

<!
[ <> &
  '%[ &
  ^-> & !&
  '] &
]@

<^!
[ ^^ &
  <> &
  <! &
  _ &
]@