"Does this work?" print !
0 ^ !
?eo !
~       ; ( -- [] )
[ 0 ^ & ; []
] @o !

~ !
?eo !
?~eo    ; ( -- op-env )
[ ~ &   ; []
  ?eo & ; op-env
] @o !

?~eo !
^^      ; ( -- [] )
[ 1 ^ & ; []
] @o !

?~eo !
^<-    ; ( [ ... val -- val [ ... )
[ ^^ & ; [ ... val []
  <> & ; [ ... [] val
  <- & ; val [ ...
] @o !

?~eo !
^->    ; ( val [ ... -- [ ... val )
[ ^^ & ; val [ ... []
  -> & ; [ ... val
] @o !

?~eo !
@~eo    ; ( op-env -- )
[ ~ &   ; op-env []
  <> &  ; [] op-env
  @eo & ;
] @o !

?~eo !
@~o      ; ( sym op -- )
[ >r &   ; sym
  ?~eo & ; sym op-env
  <> &   ; op-env sym
  r> &   ; op-env sym op
  @o &   ;
] @o !

?~o      ; ( oname -- thread )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  ?o &   ; thread
] @~o !

?~ev    ; ( -- var-env )
[ ~ &   ; []
  ?ev & ; var-env
] @~o !

@~ev    ; ( var-env -- )
[ ~ &   ; var-env []
  <> &  ; [] var-env
  @ev & ;
] @~o !

@~v      ; ( vname val -- )
[ >r &   ; vname
  ?~ev & ; vname var-env
  <> &   ; var-env vname
  r> &   ; var-env vname val
  @v &   ;
] @~o !

?~v      ; ( vname -- val )
[ ?~ev & ; vname var-env
  <> &   ; var-env vname
  ?v &   ; val
] @~o !

@~o_     ; ( oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @o_ &  ;
] @~o !

@~v_     ; ( vname -- )
[ ?~ev & ; vname var-env
  <> &   ; var-env vname
  @v_ &  ;
] @~o !

@~i+     ; ( oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @i+ &  ;
] @~o !

@~i_     ; ( oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @i_ &  ;
] @~o !

@~i-     ; (oname -- )
[ ?~eo & ; oname op-env
  <> &   ; op-env oname
  @i- &  ;
] @~o !

]&     ; ( [ ... -- thread )
[ '] & ; []
  '& & ; thread
] @~o !
]& @~i+ !

]@      ; ( sym [ ... -- )
[ '] &  ; sym []
  @~o & ;
] @~o !
]@ @~i+ !

]_     ; ( [ ... -- )
[ '] & ; []
  _ &  ;
]@
]_ @~i+ !

]i       ; ( sym [ ... -- )
[ '] &   ; sym []
  >r &   ; sym
  § &    ; sym sym
  r> &   ; sym sym []
  @~o &  ; sym
  @~i+ & ;
]@
]i @~i+ !

@~n    ; ( sym -- )
[ ~ &  ; sym []
  <> & ; [] sym
  @n & ;
]@

?~n    ; ( -- sname )
[ ~ &  ; []
  ?n & ; sname
]@

!&      ; ( op -- op {!} )
[ ''! & ; op {!}
]i

<?>!
[ <?> '& &
  '!& &
]i

?~_
[ ~ &
  ?_ &
]@

__
[ ?~_ &
    ||
    [ _ &
      __ !&
    ]
  <?>!
]@

{
[ '[ &
     ^-> &
     @~n &
]i

&&
[ ''& &
]i

}
[ '] &
  '[ &
     ''[ &
         ?~eo &
         @eo+ &&
         @~eo &&
         ?~ev &
         @ev+ &&
         @~ev &&
         ^-> &
         § &
         ^<- &
         ?n &
         @~n &&
         ^-> & &&
     '']_ &
  '] &
]i

}&
[ '} &
  &&
]i

}@
[ '] &
  § &
  ?n &
  <> &
  '%[ &
  '} &
  @~o &
]i

&@v
[ @~v &&
]i

&v
[ ?~v &&
]i

arg
[ ^-> &&
  '&@v &
]i

<<§
[ >r &
  § &
  r> &
]@

<>§
[ <<§ &
  <> &
]@

<<>
[ <<§ &
  <- &
  <> &
]@

<>>
[ § &
  -> &
  <> &
]@

?^ev
[ ?~ev &
  ?ev- &
]@

?^v
[ ?^ev &
  <> &
  ?v &
]@

&^v
[ ?^v &&
]i

@^v
[ >r &
  ?^ev &
  <> &
  r> &
  @v &
]@

<!
[ <> &
  '%[ &
  ^-> & !&
  '] &
]@

<^!
[ ^^ &
  <> &
  <! &
]@